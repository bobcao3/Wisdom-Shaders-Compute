#include "/libs/compat.glsl"

layout (local_size_x = 16, local_size_y = 16) in;

const vec2 workGroupsRender = vec2(0.55f, 0.55f);

uniform sampler2D colortex4;
uniform sampler2D colortex5;
uniform sampler2D colortex7;

layout (r11f_g11f_b10f) uniform image2D colorimg5;

f16 gaussian[] = f16[] (
    0.06136, 0.24477, 0.38774, 0.24477, 0.06136
);

uniform float viewWidth;
uniform float viewHeight;

int getIndex(ivec2 uv)
{
    return uv.y * int(gl_WorkGroupSize.x + 4) + uv.x;
}

shared f16vec3 lds_normals[(gl_WorkGroupSize.x + 4) * (gl_WorkGroupSize.y + 4)];
shared f16vec3 lds_colors[(gl_WorkGroupSize.x + 4) * (gl_WorkGroupSize.y + 4)];
shared f16 lds_variance[(gl_WorkGroupSize.x + 4) * (gl_WorkGroupSize.y + 4)];
shared float lds_depth[(gl_WorkGroupSize.x + 4) * (gl_WorkGroupSize.y + 4)];
shared f16 lds_dz[(gl_WorkGroupSize.x + 4) * (gl_WorkGroupSize.y + 4)];

uniform float near;
uniform float far;

float linearizeDepth(in float d) {
    return d;
    // return (2 * near) / (far + near - (d * 2.0 - 1.0) * (far - near));
}

void load(ivec2 local_xy, ivec2 iuv)
{
    ivec2 sample_uv = clamp(iuv, ivec2(0), ivec2(floor(viewWidth * 0.5) - 1, floor(viewHeight * 0.5) - 1));
    float depth = texelFetch(colortex4, sample_uv, 0).r;

    bool valid = depth < 1.0 && sample_uv == iuv;

    if (!valid)
    {
        lds_depth[getIndex(local_xy + 2)] = INF;
        return;
    }
    else
    {
        lds_depth[getIndex(local_xy + 2)] = linearizeDepth(depth);
    }

    lds_dz[getIndex(local_xy + 2)] = f16(4.0 * max(
        abs(depth - texelFetchOffset(colortex4, sample_uv, 0, ivec2(1, 0)).r),
        abs(depth - texelFetchOffset(colortex4, sample_uv, 0, ivec2(0, 1)).r)
    ));

    f16vec3 sample_normal = f16vec3(texelFetch(colortex7, sample_uv * 2, 0).rgb);
    f16vec3 sample_color = f16vec3(imageLoad(colorimg5, (sample_uv + IN_OFFSET)).rgb);
    f16 sample_variance = f16(imageLoad(colorimg5, (sample_uv + IN_OFFSET) + ivec2(viewWidth * 0.5, 0.0)).r);

    lds_colors[getIndex(local_xy + 2)] = sample_color;
    lds_normals[getIndex(local_xy + 2)] = sample_normal;
    lds_variance[getIndex(local_xy + 2)] = sample_variance;
}

void main()
{
    const ivec2 group_base = ivec2((gl_WorkGroupSize.xy * STRIDE) * (gl_WorkGroupID.xy / STRIDE) + (gl_WorkGroupID.xy % STRIDE));
    const ivec2 iuv = group_base + ivec2(gl_LocalInvocationID.xy * STRIDE);
    const ivec2 local_id = ivec2(gl_LocalInvocationID.xy) + 2;

    // Load the padding
    {
        int index = int(gl_LocalInvocationIndex);
        const int padded_width = int(gl_WorkGroupSize.x + 4);
        const int local_size = int(gl_WorkGroupSize.x * gl_WorkGroupSize.y);
        const int block_size = int((gl_WorkGroupSize.x + 4) * (gl_WorkGroupSize.y + 4));

        while (index < block_size)
        {
            ivec2 local_xy = ivec2((index % padded_width) - 2, (index / padded_width) - 2);
            ivec2 pad_uv = group_base + local_xy * STRIDE;
            load(local_xy, pad_uv);

            index += local_size;
        }
    }

    memoryBarrierShared();
    barrier();

    const float center_depth = lds_depth[getIndex(local_id)];
    if (center_depth == INF) return;

    f16vec3 color = f16vec3(0.0);
    f16vec3 color_backup = f16vec3(0.0);
    f16 total_var = 0.0;
    f16 weight = 0.0001;

    const f16vec3 center_normal = lds_normals[getIndex(local_id)];
    const f16vec3 center_color = lds_colors[getIndex(local_id)];
    const f16 center_luma = dot(center_color, f16vec3(0.2126, 0.7152, 0.0722));
    const f16 center_var = lds_variance[getIndex(local_id)];

    // Filter
    for (int i = -2; i <= 2; i++)
    {
        for (int j = -2; j <= 2; j++)
        {
            const ivec2 offset = ivec2(i, j);
            const float sample_depth = lds_depth[getIndex(local_id + offset)];

            if (sample_depth == INF) continue;

            const f16vec3 sample_color = lds_colors[getIndex(local_id + offset)];
            const f16vec3 sample_normal = lds_normals[getIndex(local_id + offset)];
            const f16 sample_var = lds_variance[getIndex(local_id + offset)];

            const f16 color_diff = abs(dot(sample_color, f16vec3(0.2126, 0.7152, 0.0722)) - center_luma);

            const f16 w_gaussian = gaussian[i + 2] * gaussian[j + 2];
            const f16 w_normal = pow(max(f16(0.0), dot(center_normal, sample_normal)), f16(32.0));
            const f16 w_luma = exp(-color_diff / max(sqrt(center_var), 0.01));
            const f16 w_depth = exp(-f16(abs(sample_depth - center_depth)) / (lds_dz[getIndex(local_id + offset)] * f16(max(abs(i), abs(j))) + f16(0.0001)));

            const f16 sample_weight = w_gaussian * w_normal * w_luma * w_depth;

            color += sample_color * sample_weight;
            color_backup += sample_color * gaussian[i + 2] * gaussian[j + 2];
            total_var += sample_var * pow2(sample_weight);
            weight += sample_weight;
        }
    }

    if (weight < 0.17)
    {
        color = color_backup;
        weight = 1.0;
    }

    imageStore(colorimg5, iuv + OUT_OFFSET, vec4(color / weight, 0.0));
    imageStore(colorimg5, iuv + ivec2(viewWidth * 0.5, 0.0) + OUT_OFFSET, vec4(total_var / pow2(weight)));

}